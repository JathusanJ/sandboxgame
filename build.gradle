plugins {
	id 'java'
	id 'com.gradleup.shadow' version '9.0.0-beta13'
}

project.ext.lwjglVersion = "3.3.6"
project.ext.jomlVersion = "1.10.8"
project.ext.lwjglNatives = "natives-windows"
project.ext.imguiVersion = "1.87.2"

// https://gist.github.com/JonasGroeger/7620911
def getCheckedOutGitCommitHash() {
	def gitFolder = "$projectDir/.git/"
	def takeFromHash = 8
	/*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
	def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
	def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
	// def isRef = head.length > 1     // ref: refs/heads/master

	if(isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

	def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
	refHead.text.trim().take takeFromHash
}

group = ""
version = "0.2.1 (dev)"
project.ext.git = getCheckedOutGitCommitHash()
project.ext.versionName = version

repositories {
	mavenCentral()
}

dependencies {
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	implementation "org.lwjgl:lwjgl"
	implementation "org.lwjgl:lwjgl-assimp"
	implementation "org.lwjgl:lwjgl-glfw"
	implementation "org.lwjgl:lwjgl-nfd"
	implementation "org.lwjgl:lwjgl-openal"
	implementation "org.lwjgl:lwjgl-opengl"
	implementation "org.lwjgl:lwjgl-stb"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
	implementation "org.joml:joml:${jomlVersion}"

	implementation "io.github.spair:imgui-java-app:${imguiVersion}"
	implementation "io.github.spair:imgui-java-lwjgl3:${imguiVersion}"
	implementation "io.github.spair:imgui-java-binding:${imguiVersion}"
	implementation "io.github.spair:imgui-java-natives-windows:${imguiVersion}"

	implementation "com.google.code.gson:gson:2.11.0"

	implementation project("engine")

	implementation("io.netty:netty-all:4.1.121.Final")

	implementation platform('org.apache.logging.log4j:log4j-bom:2.24.3')
	implementation "org.apache.logging.log4j:log4j-api"
	runtimeOnly 'org.apache.logging.log4j:log4j-core'
	runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl'
	implementation "org.slf4j:slf4j-api:2.0.17"
}

processResources {
	File cachedVersionJson = new File("build/resources/main/version.json");
	if(cachedVersionJson.exists()) {
		cachedVersionJson.delete();
	}

	filesMatching("version.json") {
		expand "versionId": version, "git": project.ext.git, "versionName": project.ext.versionName
	}
}

test {
	useJUnitPlatform()
}